import org.mozilla.javascript.ErrorReporter;
import org.mozilla.javascript.EvaluatorException;
import com.yahoo.platform.yui.compressor.JavaScriptCompressor


// setting up the yui dependenacy for the build script itself
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.yahoo.platform.yui:yuicompressor:2.4.6'
    }
}


// compile all of the HTML templates into pre-compiled Javascript templates
task compileTemplates
// setting up some custom properties. The gradle docs suggested placing task properties in an 'ext' object, but
// I would always receive compilation errors. This way seems to work, even if it's not convention
compileTemplates.TemplateAgent = "Handlebars" //name of the your templating class. I use Handlebars, but you could use Hogan with success
compileTemplates.appName = "MyApp" //name of the App's namespace. TODO: have the build script pull this out of the JS files

compileTemplates.doFirst {
    def files = gatherHTMLFiles()
    def templateFile = new File("js/src/templates/templates.js")
    
    templateFile.withWriter { writer->

        files.each { file ->
            println "Compiling ${file.plainName()}"
            writer.write("${compileTemplates.appName}.Templates.${file.plainName()}=${compileTemplates.TemplateAgent}.compile(\"")
            writer.write(compileHTML(file.text))
            writer.write("\");\n")

        }
    }
}


// compress and minify all of the javascript into one file
task compress(dependsOn: compileTemplates) {

    rootJS = "js/src/app.js"//your 'root' file which sets up the namespace

    doFirst {
        
        def files = gatherJavaScriptFiles([new File(compress.rootJS)] as Set)
        def appFile = new File("js/app-min.js")

        appFile.withWriter { writer->
            files.each { file->
                file.withReader { reader->
                    println "Compressing file ${file}"
                    def compressor = new JavaScriptCompressor(reader, new SimpleErrorReporter())
                    compressor.compress(writer, 1000, true, false, true, false)
                }
            }
        }
    }
}


// task which generates a javascript file that relies on head.js to include all the javascript files in the project
// Intended for development purposes, so one does not have to manually add their individually created files to the head.js
task generateDevelopment(dependsOn: compileTemplates) {

    rootJS = "js/src/app.js" // the 'root' file which sets up the namespace

    def files = gatherJavaScriptFiles([new File(compress.rootJS)] as Set)
    def devFile = new File("js/app-development.js")

    devFile.withWriter { writer->
        writer.write "//// This file was automatically generated. Run 'gradle generateDevelopment' to rebuild\n"
        writer.write "head.js(\n\t\""
        writer.write files.join("\",\n\t\"")
        writer.write "\", function () {" +compileTemplates.appName +".headComplete();})"
    }
}

//meta class a plainFile name on File

File.metaClass.plainName = {->
    String name = delegate.getName()
    int pos = name.indexOf(".")
    if (pos > 0) {
        name = name[0..(pos-1)]
    }
    name
}

//functions

List gatherJavaScriptFiles (Set defaultSet) {
    gatherReadeableFilesByType(defaultSet, ".js")
}

List gatherHTMLFiles(Set defaultSet) {
    gatherReadeableFilesByType(defaultSet, ".html")
}

// method to search the js/src folder tree
List gatherReadeableFilesByType(Set files, String type) {
    if(!files) {
        files = [] as Set
    }
    new File("js/src").traverse() { file->
        // only return readable files of our requested type
        if (file.isFile() && file.canRead() && file.getName().endsWith(type)) {
            files.add(file)
        }
    }
    files.toList()  
}

// converts a given markup string and converts it into an excaped javascript string, ready for compiling
String compileHTML(String text) {
    text.trim().replace("\n", "").replace("\"", "\\\"");
}

// the YUICompressor requires a class which implements ErrorReporter. 
class SimpleErrorReporter implements ErrorReporter {
    public void warning(String message, String sourceName, int line, String lineSource, int lineOffset) {
        println("***WARNING: ${message}")
        println("Run with --info for more information")
    }
 
    public void error(String message, String sourceName, int line, String lineSource, int lineOffset) {
        println("***ERROR: ${message}")
        println("Run with --info for more information")
    }
 
    public EvaluatorException runtimeError(String message, String sourceName, int line, String lineSource, int lineOffset) {
        error(message, sourceName, line, lineSource, lineOffset);
        return new EvaluatorException(message);
    }
}
